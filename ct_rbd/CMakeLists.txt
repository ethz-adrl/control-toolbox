cmake_minimum_required (VERSION 3.3)

include(${CMAKE_CURRENT_SOURCE_DIR}/../ct/cmake/compilerSettings.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/../ct/cmake/explicitTemplateHelpers.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/../ct/cmake/clang-cxx-dev-tools.cmake)

project(ct_rbd VERSION 3.0.0 LANGUAGES CXX)


set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wfatal-errors -std=c++14 -Wall -Wno-unknown-pragmas")
SET(CMAKE_EXPORT_COMPILE_COMMANDS TRUE)

find_package(kindr REQUIRED)
find_package(ct_optcon REQUIRED)

## add compiler definitions exported by ct_optcon
add_definitions(${ct_optcon_COMPILE_DEFINITIONS})


###################
# BUILD LIBRARIES #
###################

## define the directories to be included in all ct_rbd targets
set(ct_rbd_target_include_dirs
    ${kindr_INCLUDE_DIRS}
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/ct>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/ct/rbd>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/ct/rbd/robot/kinematics/ikfast>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/include/ct>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/include/ct/rbd>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/include/ct/rbd/robot/kinematics/ikfast>
)


## create ct_rbd library
add_library(ct_rbd SHARED src/ct_rbd.cpp)
target_include_directories(ct_rbd PUBLIC ${ct_rbd_target_include_dirs})
target_link_libraries(ct_rbd PUBLIC ct_optcon)


###########
# TESTING #
###########

if(BUILD_TESTS)
    #find_package(GTest QUIET)
    enable_testing()
    add_subdirectory(test)
endif()


#################
# INSTALLATION  #
#################

# for correct libraries locations across platforms
include(GNUInstallDirs) 

## get all compile definitions registered so far and create variable
get_directory_property( CompileDefs DIRECTORY ${CMAKE_SOURCE_DIR} COMPILE_DEFINITIONS )
foreach( d ${CompileDefs} )
    set(ct_rbd_compile_definitions  ${ct_rbd_compile_definitions} -D${d} ) # recursively add compile definitions
endforeach()

## generate files required for installation
configure_file(cmake/ct_rbdConfig.cmake.in "${PROJECT_BINARY_DIR}/ct_rbdConfig.cmake" @ONLY)

## copy the header files
install(DIRECTORY include/ct/rbd DESTINATION include/ct)
install(DIRECTORY include/ct/iit DESTINATION include/ct)

## copy the cmake files required for find_package()
install(FILES "${PROJECT_BINARY_DIR}/ct_rbdConfig.cmake" DESTINATION "share/ct_rbd/cmake")

## install library and targets
install(
    TARGETS ct_rbd
    EXPORT ct_rbd_export
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_LIBDIR}
    )

## create the ct_rbd.cmake file which holds target includes and dependencies
install (EXPORT ct_rbd_export DESTINATION ${CMAKE_INSTALL_PREFIX}/share/ct_rbd/cmake)


#################
# DOCUMENTATION #
#################
add_subdirectory(doc)
